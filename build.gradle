plugins {
  id 'base'
  id 'distribution'
  id 'java-library'
  id "com.dua3.gradle.jpms" version "0.9.0"
  id 'antlr'
  id 'eclipse'
  id "org.jetbrains.kotlin.jvm" version "1.3.71"
  id "org.jetbrains.kotlin.kapt" version "1.3.71"
  id 'org.jetbrains.dokka' version '0.10.1'
  id "com.palantir.graal" version "0.6.0-112-gca0b727"
  id "com.hpe.kraal" version "0.0.15"
  id 'maven-publish'
}

version = '0.0.0'

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  antlr "org.antlr:antlr4:4.8"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  api "org.graalvm.truffle:truffle-api:20.0.0"
  compileOnly "org.graalvm.truffle:truffle-dsl-processor:20.0.0"
  annotationProcessor "org.graalvm.truffle:truffle-dsl-processor:20.0.0"
  compileOnly "com.mageddo.nativeimage:reflection-config-generator:2.1.1"
  annotationProcessor "com.mageddo.nativeimage:reflection-config-generator:2.1.1"
  implementation "org.graalvm.sdk:graal-sdk:20.0.0"
  implementation "com.ibm.icu:icu4j:66.1"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.1"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
  testImplementation "org.graalvm.truffle:truffle-tck:20.0.0"
  testImplementation "org.junit.vintage:junit-vintage-engine:5.6.1"
}

graal {
  mainClass 'com.pthariensflame.truffle_dhall.shell.DhallMain'
  outputName 'truffle-dhall'
  graalVersion '20.0.0'
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
  withJavadocJar()
  withSourcesJar()
}

generateGrammarSource {
  arguments += ["-encoding", "UTF-8",
                "-visitor",
                "-listener",
                "-long-messages",
                "-package", "com.pthariensflame.truffle_dhall.parser.antlr"]
}

compileKotlin {
  kotlinOptions {
    languageVersion = "1.4 (EXPERIMENTAL)"
    apiVersion = "1.4 (EXPERIMENTAL)"
    javaParameters = true
    jvmTarget = "11"
  }
}

kapt {
    correctErrorTypes = true
}

dokka {
    outputFormat = 'html' 
    outputDirectory = "$docsDir/dokka"
}
