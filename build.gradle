plugins {
  id 'java-library'
  id 'eclipse'
  id 'distribution'
  id 'maven-publish'
  id "org.jetbrains.kotlin.jvm"
  id "org.jetbrains.kotlin.kapt"
  id "org.jetbrains.dokka"
  id "com.palantir.graal"
  id "com.hpe.kraal"
  id "org.javamodularity.moduleplugin"
}

version = '0.0.0'

repositories {
  maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
  maven { url 'https://kotlin.bintray.com/kotlinx' }
  mavenCentral()
  jcenter()
  gradlePluginPortal()
}

class AlignmentRules implements ComponentMetadataRule {
  void execute(ComponentMetadataContext ctx) {
    ctx.details.with {
      if (id.group.startsWith("org.graalvm")) {
        belongsTo("org.graalvm:graalvm-virtual-platform:${id.version}")
      }
      if (id.group.startsWith("org.jetbrains.kotlin")) {
        belongsTo("org.jetbrains.kotlin:kotlin-bom:${id.version}", false)
      }
      if (id.group.startsWith("com.fasterxml.jackson")) {
        belongsTo("com.fasterxml.jackson:jackson-bom:${id.version}", false)
      }
    }
  }
}

test {
  useJUnitPlatform()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
  withJavadocJar()
  withSourcesJar()
}

dokka {
  outputFormat = 'html' 
  outputDirectory = "$docsDir/dokka"
}

compileKotlin {
  kotlinOptions {
    languageVersion = "1.4 (EXPERIMENTAL)"
    apiVersion = "1.4 (EXPERIMENTAL)"
    javaParameters = true
    jvmTarget = "11"
  }
}

kapt {
  correctErrorTypes = true
  includeCompileClasspath = false
  javacOptions {
    option("--module-path", compileJava.classpath.asPath)
  }
}

graal {
  mainClass 'com.pthariensflame.truffle_dhall.shell.DhallMain'
  outputName 'truffle-dhall'
  graalVersion "${graalVMVersion}"
}

dependencies {
  components.all(AlignmentRules)
  api enforcedPlatform("org.jetbrains.kotlin:kotlin-bom:${kotlinVersion}")
  api enforcedPlatform("org.graalvm:graalvm-virtual-platform:${graalVMVersion}")
  testImplementation platform("org.junit:junit-bom:${junit5Version}")
  implementation platform("com.fasterxml.jackson:jackson-bom:[2.10.3,2.10.4)")
  constraints {
    compileOnly "com.mageddo.nativeimage:reflection-config-generator:${reflConfGenVersion}"
    implementation "com.ibm.icu:icu4j:[66.1,)"
    implementation "org.abego.treelayout:org.abego.treelayout.core:[1.0.3,)"
    implementation "org.glassfish:javax.json:[1.1.4,)"
    implementation "org.antlr:antlr4-runtime:[4.8-1,)"
    implementation "org.parboiled:parboiled-java:${parboiledVersion}"
    implementation "org.parboiled:parboiled-core:${parboiledVersion}"
  }

  api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
  implementation "org.parboiled:parboiled-java:${parboiledVersion}"
  api "org.graalvm.truffle:truffle-api:${graalVMVersion}"
  implementation "org.graalvm.sdk:graal-sdk:${graalVMVersion}"
  compileOnly "org.graalvm.truffle:truffle-dsl-processor:${graalVMVersion}"
  annotationProcessor "org.graalvm.truffle:truffle-dsl-processor:${graalVMVersion}"
  kapt "org.graalvm.truffle:truffle-dsl-processor:${graalVMVersion}"
  compileOnly "com.mageddo.nativeimage:reflection-config-generator:${reflConfGenVersion}"
  annotationProcessor "com.mageddo.nativeimage:reflection-config-generator:${reflConfGenVersion}"
  kapt "com.mageddo.nativeimage:reflection-config-generator:${reflConfGenVersion}"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:${kotlinVersion}"
  testImplementation "org.graalvm.truffle:truffle-tck:${graalVMVersion}"
  testImplementation "org.junit.vintage:junit-vintage-engine:${junit5Version}"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junit5Version}"
  testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5Version}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit5Version}"
}

sourceSets {
  main {
    java {
      srcDir 'build/generated-src/antlr/main'
    }
  }
  test {
    java {
      srcDir 'build/generated-src/antlr/test'
    }
  }
}
